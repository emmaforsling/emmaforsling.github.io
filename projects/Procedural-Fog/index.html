
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>TNM084 Project</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		
		<style>
			body {
				color: #000;
				font-family:Monospace;
				font-size:13px;
				text-align:center;
				font-weight: bold;

				background-color: #fff;
				margin: 0px;
				overflow: hidden;
			}

			#container {
			background: #fff;
			background-color: #fff;
		}

			#info {
				color:#000;
				position: absolute;
				top: 0px; width: 50%;
				padding: 5px;
			}

			a {
				color: red;
			}
		</style>

		<script src="//code.jquery.com/jquery-1.12.0.min.js"></script>
		<script type="text/javascript" src="js/libs/dat.gui.js"></script>

		<!-- GUI -->
		<script type="text/javascript">
		
		/* 	Function that initializes all variables for the gui window
		* 		When using dat.gui:
		*		this.variableName = float 		--> field with slider
		*		this.variableName = false/true 	--> checkbox field
		*		this.variableName = "string"	--> string field (in which input/output can be added)
		*											HOWEVER can also be used for the dropdown list
		*											describing which item that shall be displayed first.
		**/
		GuiContent = function() {
			this.Radius = 2.0;			 // radius for the fog, uniform: radius
			this.Mystery = false;		 // activates mysteryFunction() in the fragment shader
										 // uniform: mystery_function
			this.NoiseFrequency = 0.066; // How much noise that shall be created. uniform: noise_frequency
			this.Noise = "SimplexNoise"; // First item on the dropdown list
			this.AddNoise = 'None'; 	 // dropdown list for the color/thickness of the fog
			this.NoiseMagnitude = 0.066; // magnitude for the noise in the dropdown list (color/thickness of the fog)
			this.Name = "Emma Forsling Parborg"; 
			this.AlternateFog = 'ExponentialFog'; // dropdown list which enables the user to change between linear and exponential fog
		};

		var gui_content;
		var gui;

		/**
		*	The following function creates the GUI window for the program.
		*	The following variables can be changed by the user. Changes are registered
		*	in myScene.js in the functions onDocumentMouseDown(event) and render() in which
		*	the uniforms (for the shader) for them are updated.
		**/
		window.onload = function() {
			// Create the gui_content in which all variables that shall be displayed
			// 	in the gui window shall be initialized.
			gui_content = new GuiContent();

			gui = new dat.GUI();
			gui.width = 300;

			// Add item to the GUI
			gui.add(gui_content,"Name");
			
			// Add a folder to the gui (First folder)
			var f1 = gui.addFolder('Fog Shape');
			f1.add(gui_content, 'Noise', [ 'SimplexNoise', 'PerlinNoise'] ); // dropdown list containing
																			 // SimplexNoise and PerlinNoise
			f1.add(gui_content, 'NoiseFrequency', 0.0, 0.1);				 // intervall [0.0, 1.0]
			f1.add(gui_content, 'Radius', 0.1, 10.0);
			f1.add(gui_content, "AlternateFog", ['LinearFog', 'ExponentialFog']);

			// Add another folder to the gui (Second folder)
			var f2 = gui.addFolder('Fog color');
			f2.add(gui_content, "AddNoise", ['None', 'SimplexNoise', 'PerlinNoise']);
			f2.add(gui_content, "NoiseMagnitude", 0.0, 0.1);

			// Add another folder to the gui (Third folder)
			var f3 = gui.addFolder('Other');
			f3.add(gui_content, 'Mystery');


		};
		</script>
		
	</head>

	<body>
		<div id="container">
			<!-- Here some cool stuff will appear! -->
		</div>
			<h1> Find the secret message! </h1>
		
		<!-- **************************************************************************************
		*********************************************** SHADERS *********************************** 
		*******************************************************************************************-->

		<!-- Vertex shader for the walls, windowframe, doors etc. -->
		<script type="x-shader/x-vertex" id="vertexshader-walls">
			#ifdef GL_ES
			precision highp float;
			#endif
			
			void main() {	  
			  gl_Position  = projectionMatrix * modelViewMatrix * vec4(position,1.0);
			}

		</script>

		<!-- Fragment shader for the walls, windowframe, doors etc. -->
		<script type="x-shader/x-fragment" id="fragmentshader-walls">
			#ifdef GL_ES
			precision highp float;
			#endif

			uniform int wall;

			void main()
			{
				vec4 diffuseLightColor  = vec4(0.0, 0.0, 0.0, 1.0);
				//Different colors for
				if(wall == 0){									
					diffuseLightColor = vec4(0.3, 0.3, 0.3, 1.0);	// walls
				} else if(wall == 1){							
					diffuseLightColor = vec4(0.5, 0.5, 0.5, 1.0);	// floor, ceiling
				} else {
					diffuseLightColor = vec4(0.0,0.0,0.0, 1.0);		// door, windowframes
				}

				gl_FragColor =  diffuseLightColor;
				
			}

		</script>

		<!-- Vertex shader for the windowpane -->
		<script type="x-shader/x-vertex" id="vertexshader">
			// switch on high precision floats
			#ifdef GL_ES
			precision highp float;
			#endif
			
			uniform float time;			
			
			varying vec4 fragWorldPos;

			// varying mat4 test;
			void main()
			{		
				gl_Position = projectionMatrix * modelViewMatrix * vec4(position.xyz, 1.0);
				fragWorldPos = modelMatrix 	  * vec4(position, 1.0); // gl_Position * modelMatrix;
			}
			
		</script>

		<!-- Fragment shader for the windowpane -->
		<script type="x-shader/x-fragment" id="fragmentshader">
		
		#ifdef GL_ES
		precision highp float;
		#endif

		
	/** ================================================================== **/ 
	/*							NOISE 										*/ 
	/** ================================================================== **/ 

		// GLSL textureless classic 3D noise "cnoise",
		// with an RSL-style periodic variant "pnoise".
		// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
		// Version: 2011-10-11
		//
		// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
		// Distributed under the MIT license. See LICENSE file.
		// https://github.com/ashima/webgl-noise

		vec3 mod289(vec3 x) {
			return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 mod289(vec4 x) {
			return x - floor(x * (1.0 / 289.0)) * 289.0;
		}

		vec4 permute(vec4 x) {
			return mod289(((x*34.0)+1.0)*x);
		}

		vec4 taylorInvSqrt(vec4 r)
		{
			return 1.79284291400159 - 0.85373472095314 * r;
		}

		vec3 fade(vec3 t) {
			return t*t*t*(t*(t*6.0-15.0)+10.0);
		}

		/** ====================================== **/ 
		/*			Classic Perlin Noise 			*/ 
		/** ====================================== **/ 
		float cnoise(vec3 P)
		{
		  vec3 Pi0 = floor(P); // Integer part for indexing
		  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
		  Pi0 = mod289(Pi0);
		  Pi1 = mod289(Pi1);
		  vec3 Pf0 = fract(P); // Fractional part for interpolation
		  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
		  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
		  vec4 iy = vec4(Pi0.yy, Pi1.yy);
		  vec4 iz0 = Pi0.zzzz;
		  vec4 iz1 = Pi1.zzzz;

		  vec4 ixy = permute(permute(ix) + iy);
		  vec4 ixy0 = permute(ixy + iz0);
		  vec4 ixy1 = permute(ixy + iz1);

		  vec4 gx0 = ixy0 * (1.0 / 7.0);
		  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
		  gx0 = fract(gx0);
		  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
		  vec4 sz0 = step(gz0, vec4(0.0));
		  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
		  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

		  vec4 gx1 = ixy1 * (1.0 / 7.0);
		  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
		  gx1 = fract(gx1);
		  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
		  vec4 sz1 = step(gz1, vec4(0.0));
		  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
		  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

		  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
		  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
		  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
		  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
		  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
		  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
		  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
		  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

		  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
		  g000 *= norm0.x;
		  g010 *= norm0.y;
		  g100 *= norm0.z;
		  g110 *= norm0.w;
		  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
		  g001 *= norm1.x;
		  g011 *= norm1.y;
		  g101 *= norm1.z;
		  g111 *= norm1.w;

		  float n000 = dot(g000, Pf0);
		  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
		  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
		  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
		  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
		  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
		  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
		  float n111 = dot(g111, Pf1);

		  vec3 fade_xyz = fade(Pf0);
		  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
		  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
		  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
		  return 2.2 * n_xyz;
		}

			
		/** ====================================== **/ 
		/*				Simplex Noise  				*/ 
		/** ====================================== **/

		// Description : Array and textureless GLSL 2D/3D/4D simplex 
		//               noise functions.
		//      Author : Ian McEwan, Ashima Arts.
		//  Maintainer : ijm
		//     Lastmod : 20110822 (ijm)
		//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
		//               Distributed under the MIT License. See LICENSE file.
		//               https://github.com/ashima/webgl-noise

		float snoise(vec3 v)
		  { 
		  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
		  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

		// First corner
		  vec3 i  = floor(v + dot(v, C.yyy) );
		  vec3 x0 =   v - i + dot(i, C.xxx) ;

		// Other corners
		  vec3 g = step(x0.yzx, x0.xyz);
		  vec3 l = 1.0 - g;
		  vec3 i1 = min( g.xyz, l.zxy );
		  vec3 i2 = max( g.xyz, l.zxy );

		  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
		  //   x1 = x0 - i1  + 1.0 * C.xxx;
		  //   x2 = x0 - i2  + 2.0 * C.xxx;
		  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
		  vec3 x1 = x0 - i1 + C.xxx;
		  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
		  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

		// Permutations
		  i = mod289(i); 
		  vec4 p = permute( permute( permute( 
		             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
		           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
		           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

		// Gradients: 7x7 points over a square, mapped onto an octahedron.
		// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
		  float n_ = 0.142857142857; // 1.0/7.0
		  vec3  ns = n_ * D.wyz - D.xzx;

		  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

		  vec4 x_ = floor(j * ns.z);
		  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

		  vec4 x = x_ *ns.x + ns.yyyy;
		  vec4 y = y_ *ns.x + ns.yyyy;
		  vec4 h = 1.0 - abs(x) - abs(y);

		  vec4 b0 = vec4( x.xy, y.xy );
		  vec4 b1 = vec4( x.zw, y.zw );

		  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
		  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
		  vec4 s0 = floor(b0)*2.0 + 1.0;
		  vec4 s1 = floor(b1)*2.0 + 1.0;
		  vec4 sh = -step(h, vec4(0.0));

		  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
		  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

		  vec3 p0 = vec3(a0.xy,h.x);
		  vec3 p1 = vec3(a0.zw,h.y);
		  vec3 p2 = vec3(a1.xy,h.z);
		  vec3 p3 = vec3(a1.zw,h.w);

		//Normalise gradients
		  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
		  p0 *= norm.x;
		  p1 *= norm.y;
		  p2 *= norm.z;
		  p3 *= norm.w;

		// Mix final noise value
		  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
		  m = m * m;
		  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
		                                dot(p2,x2), dot(p3,x3) ) );
		  }
	/** ================================================================== **/ 
	/*							NOISE ENDS HERE								*/ 
	/** ================================================================== **/ 
		
		// Uniforms
		uniform float time;
		
		uniform float planeWidth;
		uniform float planeHeight;
		
		uniform vec3 cameraPosWorldSpace;
		
		uniform sampler2D hidden_Texture;		// hidden message texture
		uniform samplerCube cubeMap;			// landscape texture background
		uniform samplerCube cubeMapBlurr;		// landscape texture background

		uniform vec2 mousePosition;				
		uniform vec3 mousePositionWorldSpace;	// world coordinates
		
		// Uniforms that can be changed in the GUI
		uniform int 	mystery_function;				// activates mysterFunction() instead of Fog()
		uniform float 	noise_frequency;				// how much noise that shall be applied (noise applied to the shape of the fog)
		uniform float 	noise_frequency_for_the_color;	// how much noise that shall be applied (noise applied when determining the color of the fog)
		uniform float 	radius;							// radius of the fog
		uniform int 	noise_type_fog_shape;			// describe which noise funciton that shall be used for the fog (noise applied to the shape of the fog)
		uniform int 	noise_type_fog_color;			// describe which noise funciton that shall be used for the fog (noise applied when determining the color of the fog)

		// Varying variables set in the vertex shader
		varying vec4 fragWorldPos;
		
	
		// "Global" variables that is used in functions below
		vec3 viewDirectionWorldSpace;
		vec4 backgroundColor;			//
		vec4 hiddenMessageColor;		// The color of the hidden_Texture 
		vec4 blurryColor;				// The blurry background color derived from cubeMapBlurr


		/**
		*	Function that calls simplex noise function multiple times to get 
		* 	a pseudo random number, which it returns.
		**/
		float createSimplexNoise( vec3 coord ) {

			float n = 0.0;

			n += 0.5 * abs( snoise( coord ) );
			n += 0.25 * abs( snoise( coord * 2.0 ) );
			n += 0.125 * abs( snoise( coord * 4.0 ) );
			n += 0.0625 * abs( snoise( coord * 8.0 ) );

			return n;
		}

		/**
		*	The results from this function was created when trying to create the function fog().
		*	These functions are quite similar with the exception that this one creates perlin 
		* 	noise and applies it on the final color that this function returns.
		**/
		vec4 mysteryFunction(vec3 pos) {
			float frequency = 0.2;
			float fogValue = (cnoise(pos * frequency) + 1.0) / 2.0;				// [0,1]

			// Fog color
			const vec3 fogColor = vec3(0.7, 0.7,0.7);
			
			// distance, dependence to current camera gl_Position
			float dist = 0.0;
			dist = (gl_FragCoord.z / gl_FragCoord.w);

			// Create some noise (using the background color)
			float noisy = createSimplexNoise(vec3(backgroundColor));

			// Calculate a fogFactor for an exponential fog, inspired by link: http://in2gpu.com/2014/07/22/create-fog-shader/
			float fogFactor = 0.0;
			fogFactor = 1.0 /exp( dist);
   			fogFactor = clamp( fogFactor, 0.0, 1.0 );

   			// Create the fogColorMix 
   			vec4 fogColorMix = vec4(0.0,0.0,0.0,0.0);
   			fogColorMix = (hiddenMessageColor.x < 0.7 || hiddenMessageColor.y < 0.7 || hiddenMessageColor.z < 0.7) 	?
   			 				( vec4( mix( fogColor, vec3(blurryColor), 1.0 - fogFactor ), 1.0 ) )				: 
   							( vec4( mix( fogColor, vec3(hiddenMessageColor), 1.0 - fogFactor ), 1.0 ) );

			return ((1.0 - fogValue) * fogColorMix + fogValue * blurryColor)*noisy;
		}

		/* Function fog() */
		vec4 fog(vec3 pos) {
			// Set the noise frequency to the value from the GUI
			float frequency = noise_frequency_for_the_color;
			
			// Set fogValue to 0.5 if no noise is to be added to the color (in order to color it 0.5 with the fogColorMix and 0.5 with the blurryColor).
			// Otherwise call the noise function chosen by the user in the GUI at the intervall [0,0.5]
			float fogValue = (noise_type_fog_color == 0) ? 0.1 : ( (noise_type_fog_color == 1) ? ( (snoise(pos * frequency) + 1.0)/4.0)  : ( (cnoise(pos * frequency) + 1.0)/ 4.0) );				// intervall [0,0.5] for the simplex noise
			
			// Fog color
			const vec3 fogColor = vec3(1.0, 1.0, 1.0);

   			// The image containing the secret message is painted in black and white. The message is written in darker colors.
   			// Check if the hiddenMessageColor is < 0.7 (ergo, a secret message)
   			// 	IF TRUE: Mix the background color with a mix of fogColor and blurry color (on the secret message) 
   			// 	ELSE: 	 Mix the blurry background color with the hiddenMessageColor and then with the fogColor.
   			// The mix function works like this: mix(fogColor, blurryColor, fogFactor) == fogColor*(1-fogFactor) + blurryColor*fogFactor
   			vec4 fogColorMix = vec4(0.0,0.0,0.0,0.0);
   			fogColorMix = (hiddenMessageColor.x < 0.7 || hiddenMessageColor.y < 0.7 || hiddenMessageColor.z < 0.7) 	?
   			 				0.3 * backgroundColor + 0.7 * vec4( mix( fogColor, vec3(blurryColor), 0.5 ), 1.0 )		: 
   							(1.0 - fogValue) * vec4(fogColor, 1.0) + fogValue * backgroundColor ;//vec4(mix(fogColor, vec3(blurryColor), 0.5), 1.0);//( vec4( mix( mix( vec3(blurryColor) , vec3(hiddenMessageColor), 0.5), fogColor, 0.5 ), 1.0 ) );

			return 0.5 * fogColorMix + 0.5 * blurryColor;

		}

		/* Function noFog() will only display the background cube map since no fog is on it */
		vec4 noFog() {
			return backgroundColor;
		}


	
		void main() {
			viewDirectionWorldSpace = - normalize(cameraPosWorldSpace - fragWorldPos.xyz);
			
			/** ====================================== **/
			/*				Hidden Message 				*/
			/** ====================================== **/
			// Get normalized object coordinates [0,1]
			//   note: divided by 3.0 since the size of the windowpane is planeWidth/3.0 and planeHeight/3.0
			float px = fragWorldPos.x / (planeWidth/3.0) + 0.5;
			float py = fragWorldPos.y / (planeHeight/3.0) + 0.5;

			// Create 2D uv coordinates
			vec2 texCoord2D_hiddenMessage = vec2(px, py);

			// Sample hidden texture texture at these coordinates
			hiddenMessageColor = texture2D(hidden_Texture, texCoord2D_hiddenMessage);	// hidden texture

			/** ====================================== **/
			/*				Blurry Texture 				*/
			/** ====================================== **/
			// Create 2D uv coordinates
			blurryColor = textureCube(cubeMapBlurr, viewDirectionWorldSpace);			// blurry texture

			/** ====================================== **/
			/*		Background color from the cubeMap 	*/
			/** ====================================== **/
			backgroundColor = textureCube(cubeMap, viewDirectionWorldSpace);

			/** ====================================== **/
			/*		Create Fog shape x^2 + y^2 = r^2 	*/
			/** ====================================== **/
			// Calculate the radius for a perfect circle, with the mousePositionWorldSpace as the center/origin of the circle.
			float distanceToMouseFromFragmentPosition = sqrt( pow(fragWorldPos.x - mousePositionWorldSpace.x, 2.0) + 
												pow(fragWorldPos.y - mousePositionWorldSpace.y, 2.0) );
			
			/** ====================================== **/
			/* 				Create some noise 			*/
			/** ====================================== **/
			/** 
			* If noise_type_fog_shape == 0 : Simplex Noise
			* If noise_type_fog_shape == 1 : Perlin  Noise
			* noise_frequency - can be changed by the user in the GUI. 
			**/
			float currentNoise = (noise_type_fog_shape == 0) ? snoise(fragWorldPos.xyz * noise_frequency) + 1.5 : cnoise(fragWorldPos.xyz * noise_frequency) + 2.0;  
			// float tempNoise2 = createSimplexNoise(fragWorldPos.xyz * noise_frequency) + 1.5;
			

			/* 
			* Create and set fogOutline to the actual radius and add noise (in order to get a varied shape of the fog) 
			* This variable can then be used later in order to determine if fog or no fog shall be created, 
			* 	(by comparing it with the computed distanceToMouseFromFragment).
			*/
			float fogOutline = radius * currentNoise;			
			
			/** 
			*	Perform smoothstep() in order to get a smooth transition, with min set to 0.0 and max set to the fogOutline, 
			* 	and x to the distanceToMouseFromFragmentPosition.
			* 	smoothstep() perfroms a Hermite interpolation (between 0 and 1): returns 3t^2 + 2t^3,
			*	where t = clamp((x - min)(max - min), 0.0, 1.0).
			*  	(Note: results are undefined if max <= min)
			**/
			float smoothy = smoothstep(0.0, fogOutline, distanceToMouseFromFragmentPosition);		// if returns 1.0 then no fog, if 0.0 mycket fog
			
			/** ====================================== **/
			/* 		gl_FragColor for the windowpane 	*/
			/** ====================================== **/
			/** 
			*  	When using smoothstep: 0.0 < distanceToMouseFromFragmentPosition < fogOutline, have to be fulfilled  
			*  	Check if this is fulfilled and use the smooth transition variable smoothy in order to say which parts that should have fog or not
			*  	The mystery function can be activated by the user, if it is call this function instead of the fog function.
			**/
			gl_FragColor = (mystery_function == 0) ?
							(	(distanceToMouseFromFragmentPosition < fogOutline && distanceToMouseFromFragmentPosition > 0.0) ? (1.0 - smoothy) * fog(fragWorldPos.xyz) + smoothy * noFog() : noFog() ) :
							( 	(distanceToMouseFromFragmentPosition < fogOutline && distanceToMouseFromFragmentPosition > 0.0) ? (1.0 - smoothy) * mysteryFunction(fragWorldPos.xyz) : noFog()	);
		}
		</script>
		<!-- End Shaders -->
		
		<script src="https://ajax.googleapis.com/ajax/libs/jquery/1.5.1/jquery.min.js"></script>
		<script src="js/three.min.js"></script>
		<script src="js/controls/FlyControls.js"></script>
		<script src="js/Detector.js"></script>
		<script src="js/libs/stats.min.js"></script>
		<script src="myScene.js"></script>

	</body>
</html>
